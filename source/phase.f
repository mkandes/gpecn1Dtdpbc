! ======================================================================
! PROGRAM PHASE
! ----------------------------------------------------------------------
      PROGRAM PHASE
      IMPLICIT NONE

!     Parameter Declarations:
!     -----------------------
      REAL, PARAMETER :: PI = 3.1415926535897932384626433832795028841971E0

!     Input Variable and Array Declarations:
!     --------------------------------------
      INTEGER :: NUMBER_OF_GRID_POINTS
      INTEGER :: NUMBER_OF_TIME_STEPS
      INTEGER :: NUMBER_OF_TIME_STEPS_BEFORE_WRITE
      INTEGER :: DISTRIBUTION_FILE
      INTEGER :: START_MOMENTUM
      INTEGER :: END_MOMENTUM

      REAL :: RADIUS
      REAL :: GAMMA_FACTOR
      REAL :: ROTATION_RATE

!     Internal Variable and Array Declarations:
!     -----------------------------------------
      CHARACTER(80) :: BUFFER

      INTEGER :: CURRENT_TIME_STEP
      INTEGER :: CURRENT_MOMENTUM
      INTEGER :: LENGTH
      INTEGER :: INFO
      INTEGER :: M, N

      REAL :: ANGULAR_GRID_SPACING_SIZE
      REAL :: TIME_STEP_SIZE
      REAL :: TIME
      REAL :: X
      REAL :: Y
      REAL :: PHASESHIFT

      REAL, ALLOCATABLE, DIMENSION(:) :: COEFFICIENT

!     Input Variable Assignment Read from Command-line Arguments:
!     -----------------------------------------------------------
      CALL GET_COMMAND_ARGUMENT(1,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) NUMBER_OF_GRID_POINTS

      CALL GET_COMMAND_ARGUMENT(2,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) NUMBER_OF_TIME_STEPS

      CALL GET_COMMAND_ARGUMENT(3,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) NUMBER_OF_TIME_STEPS_BEFORE_WRITE

      CALL GET_COMMAND_ARGUMENT(4,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) DISTRIBUTION_FILE

      CALL GET_COMMAND_ARGUMENT(5,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) START_MOMENTUM

      CALL GET_COMMAND_ARGUMENT(6,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) END_MOMENTUM

      CALL GET_COMMAND_ARGUMENT(7,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) RADIUS

      CALL GET_COMMAND_ARGUMENT(8,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) GAMMA_FACTOR

      CALL GET_COMMAND_ARGUMENT(9,BUFFER,LENGTH,INFO)
      READ(BUFFER,*) ROTATION_RATE


!     Internal Variable Assignment:
!     -----------------------------
      ANGULAR_GRID_SPACING_SIZE = 2.0E0*PI/FLOAT(NUMBER_OF_GRID_POINTS)
      TIME_STEP_SIZE = (RADIUS*ANGULAR_GRID_SPACING_SIZE)**2/(2.0E0*GAMMA_FACTOR)


!     Allocate Arrays:
!     ----------------
      ALLOCATE(COEFFICIENT(START_MOMENTUM:END_MOMENTUM))

!     Main Program:
!     -------------
      OPEN(UNIT=DISTRIBUTION_FILE,ACTION='READ',FORM='FORMATTED',STATUS='OLD')
      DO CURRENT_MOMENTUM = START_MOMENTUM, END_MOMENTUM
         READ(DISTRIBUTION_FILE,*) INFO, COEFFICIENT(CURRENT_MOMENTUM), ANGULAR_GRID_SPACING_SIZE, ANGULAR_GRID_SPACING_SIZE
      ENDDO
      CLOSE(UNIT=DISTRIBUTION_FILE,STATUS='KEEP')
      COEFFICIENT = SQRT(COEFFICIENT)
      DO CURRENT_TIME_STEP = 1, NUMBER_OF_TIME_STEPS, NUMBER_OF_TIME_STEPS_BEFORE_WRITE
         X = 0.0E0
         Y = 0.0E0
         TIME = FLOAT(CURRENT_TIME_STEP)*TIME_STEP_SIZE
         DO M = START_MOMENTUM+20, END_MOMENTUM
            N = M - 2*10
            X = X + COEFFICIENT(M)*COEFFICIENT(N)*COS(((FLOAT(N)**2-FLOAT(M)**2)/(2*RADIUS**2)+(FLOAT(M)-FLOAT(N))*ROTATION_RATE)*TIME)
            Y = Y - COEFFICIENT(M)*COEFFICIENT(N)*SIN(((FLOAT(N)**2-FLOAT(M)**2)/(2*RADIUS**2)+(FLOAT(M)-FLOAT(N))*ROTATION_RATE)*TIME)
         ENDDO
         PHASESHIFT = ATAN2(Y,X)
         WRITE(6,*) TIME, PHASESHIFT
      ENDDO

!     Deallocate Arrays:
!     ------------------
      DEALLOCATE(COEFFICIENT)

      STOP
      ENDPROGRAM PHASE
! ======================================================================
